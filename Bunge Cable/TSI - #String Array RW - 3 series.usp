/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name:
System Number:
Programmer:		Jeff Pride
Comments:
*/

//*****************************************************************************************//
//*******************************		DIRECTIVES		***********************************//
//*****************************************************************************************//
#SYMBOL_NAME "TSI - #String Array Read-Write (3 or 4 Series)"
#CATEGORY "46" "#TSI Modules"
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE
#OUTPUT_SHIFT	1

#DEFINE_CONSTANT	MAXSTRINGS	100
#DEFINE_CONSTANT	MAXSTRINGSIZE	1024	

           
#USER_SIMPLSHARP_LIBRARY "TSI.ThreeSeries.FileOperations"

#HELP_BEGIN

Example of the file format:

{
    "LastUpdated": "8/26/2020 8:46:54 AM", <-- This is just a string
    "Strings": [
        "String 1",
        "String 2",
        "String 3",
        "String 4",
        "String 5",
        "String 6",
        "String 7",
        "String 8",
        "String 9",
        "String 10"
    ]
}

#HELP_END

//*****************************************************************************************//
//**********************************		I/O		***************************************//
//*****************************************************************************************//
DIGITAL_INPUT 	_skip_,
				debug,
				_skip_,
				Read,
				Write,
				_skip_,
				_skip_;

STRING_INPUT 	StringIn$[MAXSTRINGS][MAXSTRINGSIZE];
               
DIGITAL_OUTPUT	Busy,
				_skip_;

STRING_OUTPUT 	LastUpdated$,
				_skip_,
				_skip_,
				_skip_,
				StringOut$[MAXSTRINGS];

String_Parameter	filepath$[64];

#BEGIN_PARAMETER_PROPERTIES	filepath$
   	propValidUnits = unitString;
   	propDefaultUnit = unitString;
   	propDefaultValue = "/User/";
	propShortDescription = "/User/filename.json";
#END_PARAMETER_PROPERTIES

 
//*****************************************************************************************//
//**********************************	GLOBAL VARS	***************************************//
//*****************************************************************************************//
FileOperations obj;

string 	lastUpdatedFromSimplSharp[24],
		stringFromSimplSharp[MAXSTRINGS][MAXSTRINGSIZE],
		stringFromPlusToSharp[MAXSTRINGS][MAXSTRINGSIZE];

integer StringInCount, debugFlag;
    
     
//*****************************************************************************************//
//********************************		FUNCTIONS	***************************************//
//*****************************************************************************************//

function RegisterEventHandlers()
{
	RegisterEvent(obj, DeserializationSuccessEvent, OnDeserializationSuccessEvent);	
	RegisterEvent(obj, FileWriteSuccessEvent, OnFileWriteSuccessEvent);	

}
    

//*****************************************************************************************//
//******************************		EVENTHANDLERS	***********************************//
//*****************************************************************************************//
  

//Hold high for debug on, release for debug off
Threadsafe Change debug
{
	debugFlag = debug;
	obj.SetDebugEnable(debug);
	if (debugFlag) {	trace("StringInCount: %d", StringInCount);	}
}    
     

Threadsafe Push Read
{
	busy = 1;
	obj.FilePath = filepath$;
	obj.ReadFile();
	delay(100);
	busy = 0;
}
     

Threadsafe Push Write
{
	integer i;

	try
	{
		busy = 1;
        
		if (StringInCount > 0)
		{ 
			for (i = 0 to StringInCount - 1)
			{
				if (debugFlag) {	trace("loop index: %d", i);	}
				obj.UpdateListFromSimpl(i,StringIn$[i+1]);
				StringOut$[i+1] = StringIn$[i+1];
			}
	
			delay(50);
		    obj.WriteFile();
        }
		busy = 0;

	}
	catch
	{
    	trace("Write Event Exception thrown: %s",GetExceptionMessage());
	}

}
    

//eventhandler called when the simpl# app internally raises the event
eventhandler OnDeserializationSuccessEvent(FileOperations sender, DeserializationSuccessEventArgs args)
{
	integer i;
       
	try
	{
		busy = 1;
         
		if (debugFlag) {	trace("ListCount: %d",args.ListCount); }
            
		//this loops through the list of strings returned by the EventArgs associated with this event
		//it grabs the strings from args.StringArray an sets the output strings to those strings                                  
		for (i = 0 to args.ListCount - 1) 
		{
			stringFromSimplSharp[i] = args.StringArray[i];
			StringOut$[i+1] = stringFromSimplSharp[i];
		}
	
		lastUpdatedFromSimplSharp = args.LastUpdate;
		LastUpdated$ = lastUpdatedFromSimplSharp;

		busy = 0;
	}
	catch	
	{
    	trace("Deserialization Event Exception thrown: %s",GetExceptionMessage());
	}

} 

//eventhandler called when the simpl# app internally raises the event
eventhandler OnFileWriteSuccessEvent(FileOperations sender, FileWriteSuccessEventArgs args)
{
	lastUpdatedFromSimplSharp = args.LastUpdate;
	LastUpdated$ = lastUpdatedFromSimplSharp;
}


 
//*****************************************************************************************//
//**********************************		MAIN	***************************************//
//*****************************************************************************************//


function main()
{
	integer i;
            
    //Register Eventhandlers
	RegisterEventHandlers();

	//set Filepath
	obj.FilePath = filepath$;

	waitforinitializationcomplete();
        
	debugFlag = debug;

	for (i = MAXSTRINGS to 1 step -1)
	{
    	if (isSignalDefined(StringIn$[i]))
		{
        	StringInCount = i;
			break;
		}
	}              

     
}
